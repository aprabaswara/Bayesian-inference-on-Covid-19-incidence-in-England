##Aditya Prabaswara Mardjikoen (S2264710)

##Overview:
##-----------------------------------------------
##In this JAGS model, we will specify a brief
##procedure to compute the daily number of new
##infections and the expected number of daily
##deaths. We will use this following notation in
##this JAGS model:
##x[i] = log of the number of infections on day i
##n[i] = number of new infections on day i
##m[i] = expected number of deaths on day i
##y[i] = observed number of deaths on day i
##N = number of observations in the dataset
##tau = precision for day two and afterwards
##B = square matrix defined in P5S2264710.R file
##
##Assume that y[i] and x[i] has a Poisson and
##Normal distribution respectively. To be noted,
##the Normal distribution for x[i] is
##parameterized in terms ofprecision instead of
##variance. 
##-----------------------------------------------

model{

  ## Specify the distribution for x[1] and x[2].
  
  x[1] ~ dnorm(0,0.01)
  
  x[2] ~ dnorm(x[1],tau)
  
  ## Specify the distribution for the rest of x[i].
  
  for (i in 3:N){ 
  
    x[i] ~ dnorm(2*x[i-1]-x[i-2],tau)
    
  }
  
  ##Compute n[i] and m[i].
  
  for (i in 1:N){
  
    n[i] <- exp(x[i]) 
    
  }
  
  m <- B %*% n
  
  ## Specify the distribution for the y[i].
  
  for (i in 1:N){
  
    y[i] ~ dpois(m[i]) 
    
  }
  
  tau ~ dgamma(4,0.04) ## Prior for precision tau.
}